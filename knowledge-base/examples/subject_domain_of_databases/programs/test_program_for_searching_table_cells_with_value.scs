// ==========================================================================================
// ТЕСТОВАЯ ПРОГРАММА
// ==========================================================================================


test_program_for_table_cells_with_value
=> nrel_main_idtf:
	[Тестовая программа поиска клеточек таблицы, которые имеют больше 20 значение]
    (*
        <- lang_ru;;
    *);
	[Test program for searching table cells with value that's more than 20]
    (*
        <- lang_en;;
    *);
<- scp_program;
-> rrel_key_sc_element:
    .._process;;

test_program_for_table_cells_with_value = [*
.._process
<-_ scp_process;

<=_ nrel_decomposition_of_action:: _...
(*
	_-> rrel_1:: .._call_program_for_searching_needed_values
	(*
		<-_ call;;

		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: program_for_searching_cells_with_needed_values;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: .._params
		(* 
			_-> rrel_1:: rrel_fixed:: rrel_scp_const:: table_of_cells;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: .._result_cells;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: .._descriptor;;
					
		_=> nrel_goto:: .._wait_for_result_of_program_for_searching_needed_values;;
	*);;
	_-> .._wait_for_result_of_program_for_searching_needed_values
	(*
		<-_ waitReturn;;

		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: .._descriptor;;
						
		_=> nrel_goto:: .._print_result_cells;;
	*);;
	_-> .._print_result_cells
	(*
		<-_ printEl;;

		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: .._result_cells;;

		_=> nrel_goto:: .._return;;
	*);;
	_-> .._return
	(*
		<-_ return;;
	*);;
*);;
*];;
